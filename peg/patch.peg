// Symbols
file1_header_start = { "---" }
file2_header_start = { "+++" }
context_header_delimiter = { "@@" }
context_line_start = { SPACE_SEPARATOR }
deleted_line_start = { "-" }
inserted_line_start = { "+" }
path_timestamp_delimiter = { "\t" }

// Values
path = { (!"\t" ~ ANY)+ }
timestamp = { line }
number = { NUMBER+ }
file1_l = { number }
file1_s = { number }
file2_l = { number }
file2_s = { number }

// Lines
line = { (!NEWLINE ~ ANY)* ~ NEWLINE }
line_context = { context_line_start ~ line }
line_deleted = { deleted_line_start ~ line }
line_inserted = { inserted_line_start ~ line }

// Headers
file1_header = { file1_header_start ~ SPACE_SEPARATOR ~ path ~ path_timestamp_delimiter ~ timestamp }
file2_header = { file2_header_start ~ SPACE_SEPARATOR ~ path ~ path_timestamp_delimiter ~ timestamp }
context_header = {
    context_header_delimiter ~ SPACE_SEPARATOR ~
    "-" ~ file1_l ~ "," ~ file1_s ~ SPACE_SEPARATOR ~
    "+" ~ file2_l ~ "," ~ file2_s ~ SPACE_SEPARATOR ~
    context_header_delimiter ~ SPACE_SEPARATOR? ~ line
}

// Sections
context = {
    context_header ~
    (line_context | line_deleted | line_inserted)+
}
patch = {
    SOI ~
    file1_header ~
    file2_header ~
    context+ ~
    EOI
}
